buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories { 
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath ("io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE")
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    if (name.endsWith('dsl')) {
        apply plugin: 'com.ewerk.gradle.plugins.querydsl'
    }

    group = 'io.iamkyu'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'com.h2database:h2:1.4.197'

        compile 'org.postgresql:postgresql:42.2.5'
    }
}

project(':data-source') {
    bootJar { enabled = false }
    jar { enabled = true }
}


project(':relation') {
    dependencies {
        implementation project(':data-source')
    }
}

project(':spring-jpa-custom-repository') {
    dependencies {
        implementation project(':data-source')
    }
}

project(':domain-event') {
}

project(':query-dsl') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        implementation project(':data-source')
        compile 'com.querydsl:querydsl-apt:4.2.1'
        compile 'com.querydsl:querydsl-jpa:4.2.1'
    }

    def queryDslSrcDir = 'src/main/generated'

    querydsl {
        library = "com.querydsl:querydsl-apt"
        jpa = true
        querydslSourcesDir = queryDslSrcDir
    }

    configurations {
        querydsl.extendsFrom compileClasspath
    }

    compileQuerydsl {
        options.annotationProcessorPath = configurations.querydsl
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', queryDslSrcDir]
            }
        }
    }
}

project(':spring-data-jpa') {
    dependencies {
        implementation project(':data-source')
//        annotationProcessor 'org.hibernate:hibernate-jpamodelgen:5.2.12.Final'
    }

//    tasks.withType(JavaCompile) {
//        options.annotationProcessorGeneratedSourcesDirectory = file("src/main/generated")
//    }
//
//    compileJava.options.compilerArgs += [
//    ]
//
//    sourceSets {
//        generated {
//            java {
//                srcDirs = ['src/main/java', 'src/main/generated']
//            }
//        }
//    }
}
